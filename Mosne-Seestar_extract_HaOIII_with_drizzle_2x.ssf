############################################
#
# Script for Siril 1.4
# May 2025
# Mosne Seestar extract Ha and OIII with drizzle 2x v1.0
# Use this scripts with images taken with LP filter
#
# Based on 
# (C) Cyril Richard
# ExtractHaOIII v1.5
#
########### PREPROCESSING SCRIPT ###########
#
# Script for color camera preprocessing that
# extracts Ha and OIII
#
# Needs 4 sets of RAW images in the working
# directory, within 4 directories:
#   lights/
# Saves masters to ./results/
#
############################################

requires 1.3.4

# Convert Light Frames to .fit files
cd lights
link light -out=../process
cd ../process

# Pre-process Light Frames
calibrate light -cfa -equalize_cfa

# Extract Ha and OIII
seqextract_HaOIII pp_light -resample=ha

# Align Ha lights
register Ha_pp_light -2pass -transf=similarity -minpairs=5 -interp=cubic -drizzle -scale=2.0 -pixfrac=0.5 -kernel=square
seqapplyreg Ha_pp_light -interp=cubic -filter-round=2.5k -drizzle -scale=2.0 -pixfrac=0.5 -kernel=square


# Stack calibrated Ha lights to Ha_stack (temporary)
stack r_Ha_pp_light rej 3 3 -norm=addscale -output_norm -out=result -out=results_00001

# and flip if required
mirrorx_single results_00001

# Align OIII lights
register OIII_pp_light -2pass -transf=similarity -minpairs=5 -interp=cubic -drizzle -scale=2.0 -pixfrac=0.5 -kernel=square
seqapplyreg OIII_pp_light -interp=cubic -filter-round=2.5k -drizzle -scale=2.0 -pixfrac=0.5 -kernel=square

# Stack calibrated OIII lights to OIII_stack (temporary)
stack r_OIII_pp_light rej 3 3 -norm=addscale -output_norm -out=results_00002

# and flip if required
mirrorx_single results_00002

# Align the result images, small shifts and chromatic aberrations can occur
register results -transf=shift -interp=none

# Renorm OIII to Ha using PixelMath
pm $r_results_00002$*mad($r_results_00001$)/mad($r_results_00002$)-mad($r_results_00001$)/mad($r_results_00002$)*median($r_results_00002$)+median($r_results_00001$)
save ../result_OIII_$LIVETIME:%d$s

# Save Ha final result
load r_results_00001
save ../result_Ha_$LIVETIME:%d$s

#░░░█▀▄▀█░▄▀▀▄░█▀▀░█▀▀▄░█▀▀░░░█▀▀▄░█▀▀░▀█▀░█▀▀▄░▄▀▀▄
#░░░█░▀░█░█░░█░▀▀▄░█░▒█░█▀▀░░░█▄▄█░▀▀▄░░█░░█▄▄▀░█░░█
#░░░▀░░▒▀░░▀▀░░▀▀▀░▀░░▀░▀▀▀░░░▀░░▀░▀▀▀░░▀░░▀░▀▀░░▀▀░

close
